#
# yokan-config.cmake.in
#

set(YOKAN_VERSION @YOKAN_VERSION@)

@PACKAGE_INIT@

set_and_check(YOKAN_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")

check_required_components(yokan)

set(YOKAN_HAS_LEVELDB    @YOKAN_HAS_LEVELDB@)
set(YOKAN_HAS_ROCKSDB    @YOKAN_HAS_ROCKSDB@)
set(YOKAN_HAS_BERKELEYDB @YOKAN_HAS_BERKELEYDB@)
set(YOKAN_HAS_TKRZW      @YOKAN_HAS_TKRZW@)
set(YOKAN_HAS_GDBM       @YOKAN_HAS_GDBM@)
set(YOKAN_HAS_LMDB       @YOKAN_HAS_LMDB@)
set(YOKAN_HAS_UNQLITE    @YOKAN_HAS_UNQLITE@)
set(YOKAN_HAS_LUA        @YOKAN_HAS_LUA@)
set(YOKAN_HAS_REMI       @YOKAN_HAS_REMI@)

set (CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH}
     "${CMAKE_CURRENT_LIST_DIR}")

include (CMakeFindDependencyMacro)
find_dependency (PkgConfig)
pkg_check_modules (margo REQUIRED IMPORTED_TARGET margo)

if(YOKAN_HAS_LEVELDB)
    pkg_check_modules (leveldb REQUIRED IMPORTED_TARGET leveldb)
endif()

if(YOKAN_HAS_ROCKSDB)
    pkg_check_modules (rocksdb REQUIRED IMPORTED_TARGET rocksdb)
endif()

if(YOKAN_HAS_BERKELEYDB)
    find_dependency (BerkeleyDB)
endif()

if(YOKAN_HAS_TKRZW)
    pkg_check_modules (tkrzw REQUIRED IMPORTED_TARGET tkrzw)
endif()

if(YOKAN_HAS_GDBM)
    find_dependency (GDBM)
endif()

if(YOKAN_HAS_LMDB)
    pkg_check_modules (lmdb REQUIRED IMPORTED_TARGET lmdb)
endif()

if(YOKAN_HAS_UNQLITE)
    find_dependency (UnQLite)
endif()

if(YOKAN_HAS_LUA)
    find_dependency (sol2)
    find_dependency (Lua)
endif()

if(YOKAN_HAS_REMI)
    find_dependency (remi)
endif()

include ("${CMAKE_CURRENT_LIST_DIR}/yokan-targets.cmake")

if (NOT TARGET yokan::client)
add_library (yokan::client ALIAS yokan-client)
add_library (yokan::server ALIAS yokan-server)
endif ()
